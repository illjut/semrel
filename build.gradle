buildscript {
  repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/illjut/maven' }
    mavenLocal()
  }

  dependencies {
    classpath group: 'de.illjut.gradle', name: 'semrel', version: project.findProperty('semrelVersion') ?: '1+'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
  }
}

plugins {
  id 'java-gradle-plugin'
  id 'groovy'
  id 'maven-publish'
  id "com.github.ben-manes.versions" version "0.33.0"
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'de.illjut.gradle.semrel'

repositories {
  jcenter()
}

test {
  useJUnitPlatform()
}

dependencies {
  implementation gradleApi()
  implementation localGroovy()

  implementation "org.yaml:snakeyaml:1.27"
  implementation "org.ajoberstar.grgit:grgit-core:4.1.0"

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.2'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.21.0'
}

group = 'de.illjut.gradle'

gradlePlugin {
  plugins {
    semanticReleasePlugin {
      id = 'de.illjut.gradle.semrel'
      implementationClass = 'de.illjut.gradle.semrel.SemrelPlugin'
    }
  }
}

task javadocJar(type: Jar) {
  from javadoc
  archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/illjut/semrel")
      credentials {
        username = System.getenv("PUBLISH_USER") ?: ""
        password = System.getenv("PUBLISH_TOKEN") ?: "none"
      }
    }
  }

  publications {
    pluginMaven(MavenPublication) {
      pom {
        name = "Gradle Semantic Release"
        description = "semantic-release for gradle"
      }
    }
  }
}

project.tasks.release.onlyIf { !project.ext.isSnapshot }
tasks.withType(PublishToMavenRepository) {
  onlyIf { !project.ext.isSnapshot }
  dependsOn project.tasks.release
}
